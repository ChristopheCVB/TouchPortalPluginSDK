plugins {
    id 'groovy-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'maven-publish'
    id 'signing'
}

group 'com.christophecvb.touchportal'
def localArchiveBaseName = 'plugin-packager'
version isRelease ? versionName : versionName.replace('-SNAPSHOT', '-' + System.currentTimeSeconds())

pluginBundle {
    website = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
    vcsUrl = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
    description = 'Touch Portal Plugin Packager - Use with Touch Portal Plugin SDK'
    tags = ['touchportal', 'packager']
}

gradlePlugin {
    plugins {
        packagerPlugin {
            displayName = 'Touch Portal Plugin SDK Packager'
            id = group + '.' + localArchiveBaseName
            implementationClass = 'com.christophecvb.touchportal.packager.TouchPortalPluginPackager'
            description = 'Touch Portal Plugin Packager - Use with Touch Portal Plugin SDK'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        withType(MavenPublication) {
            artifactId = localArchiveBaseName
            pom {
                name = 'Touch Portal Plugin SDK Packager'
                description = 'This Project is an SDK to create a Touch Portal Plugin using Java or Kotlin and Gradle'
                url = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
                inceptionYear = '2020'

                scm {
                    url = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
                    connection = 'scm:https://github.com/ChristopheCVB/TouchPortalPluginSDK.git'
                    developerConnection = 'scm:git://github.com/ChristopheCVB/TouchPortalPluginSDK.git'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'ChristopheCVB'
                        name = 'Christophe Carvalho Vilas-Boas'
                        email = 'christophe.carvalhovilasboas@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/' : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = envOrPropOrEmpty('OSSRH_USERNAME')
                password = envOrPropOrEmpty('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = envOrPropOrEmpty('PGP_KEY')
    def signingPassword = envOrPropOrEmpty('PGP_PWD')

    useInMemoryPgpKeys(signingKey, signingPassword)

    if (signingKey != '') {
        sign publishing.publications
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
