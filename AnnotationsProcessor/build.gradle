plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.christophecvb.touchportal'
def localArchiveBaseName = 'plugin-sdk-annotations-processor'
version versionName

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = localArchiveBaseName
            from components.java
            pom {
                name = 'Touch Portal Plugin SDK Annotations Processor'
                description = 'This Project is an SDK to create a Touch Portal Plugin using Java or Kotlin and Gradle'
                url = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
                inceptionYear = '2020'

                scm {
                    url = 'https://github.com/ChristopheCVB/TouchPortalPluginSDK'
                    connection = 'scm:https://github.com/ChristopheCVB/TouchPortalPluginSDK.git'
                    developerConnection = 'scm:git://github.com/ChristopheCVB/TouchPortalPluginSDK.git'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id = 'ChristopheCVB'
                        name = 'Christophe Carvalho Vilas-Boas'
                        email = 'christophe.carvalhovilasboas@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/' : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSSRH_USERNAME') ? System.getenv('OSSRH_USERNAME') : findProperty("OSSRH_USERNAME").toString()
                password = System.getenv('OSSRH_PASSWORD') ? System.getenv('OSSRH_PASSWORD') : findProperty("OSSRH_PASSWORD").toString()
            }
        }
    }
}

signing {
    def signingKey = System.getenv('PGP_KEY') ? System.getenv('PGP_KEY') : findProperty("PGP_KEY").toString().replace("\\n", "\n")
    def signingPassword = System.getenv('PGP_PWD') ? System.getenv('PGP_PWD') : findProperty("PGP_PWD").toString()

    useInMemoryPgpKeys(signingKey, signingPassword)

    if (signingKey != '') {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'com.google.auto.service:auto-service:1.0-rc7'
    implementation group: 'com.squareup', name: 'javapoet', version: '1.12.1'
    implementation project(':Annotations')
    implementation project(':Helpers')

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}
